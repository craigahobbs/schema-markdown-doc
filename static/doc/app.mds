# Licensed under the MIT License
# https://github.com/craigahobbs/schema-markdown-js/blob/main/LICENSE


#
# The Schema Markdown documentation viewer
#


#
# url - The Schema Markdown text or JSON resource URL
# title - The schema title
#
async function schemaMarkdownDoc(url, title)
    # URL override?
    url = if(vURL != null, vURL, url)
    title = if(title != null && vURL == null, title, url)

    # If no URL was provided, use the Schema Markdown type model schema
    if url == null:
        types = schemaTypeModel()
    else:
        # Fetch the Schema Markdown resource
        if stringEndsWith(url, '.json'):
            schemaJSON = systemFetch(url)
            if schemaJSON != null:
                types = schemaValidateTypeModel(schemaJSON)
            endif
        else:
            schemaText = systemFetch(url, null, true)
            if schemaText != null:
                types = schemaParse(schemaText)
            endif
        endif

        # Error?
        if types == null:
            markdownPrint('**Error:** Failed to fetch Schema Markdown resource "' + url + '"')
            return
        endif
    endif

    # Render the page
    if vName != null:
        typePage(types, title, vName)
    else:
        indexPage(types, title)
    endif
endfunction


function indexPage(types, title)
    # Set the page title
    documentSetTitle(title)
    markdownPrint('# ' + title)

    # Group the types
    groups = objectNew()
    typeNames = arraySort(objectKeys(types))
    uncategorizedTypeGroups = objectNew('action', 'Actions', 'enum', 'Enums', 'struct', 'Structs', 'typedef', 'Typedefs')
    for typeName in typeNames:
        type = objectGet(types, typeName)
        group = objectGet(objectGet(type, arrayGet(objectKeys(type), 0)), 'docGroup')
        if group == null:
            group = objectGet(uncategorizedTypeGroups, arrayGet(objectKeys(type), 0))
        endif
        if !objectHas(groups, group):
            objectSet(groups, group, arrayNew())
        endif
        arrayPush(objectGet(groups, group), type)
    endfor

    # Render the index groups
    groupNames = arraySort(objectKeys(groups))
    for groupName in groupNames:
        markdownPrint('', '## ' + markdownEscape(groupName))

        # Render the group type links
        groupTypes = objectGet(groups, groupName)
        for groupType in groupTypes:
            groupTypeName = objectGet(objectGet(groupType, arrayGet(objectKeys(groupType), 0)), 'name')
            groupTypeURL = '#' + if(vURL != null, "var.vURL='" + vURL + "'&", '') + "var.vName='" + groupTypeName + "'"
            markdownPrint('', '[' + groupTypeName + '](' + groupTypeURL + ')')
        endfor
    endfor
endfunction


function typePage(types, title, typeName)
    # Set the page title
    documentSetTitle(title + ' - ' + typeName)
    markdownPrint('[Index](' + if(vURL != null, "#var.vURL='" + vURL + "')", '#var=)'))

    # Type exist?
    if !objectHas(types, typeName):
        markdownPrint('', '**Error:** Unknown type "' + typeName + '"')
        return
    endif

    # Render the type's documentation
    elementModelRender(schemaElements(types, typeName))
endfunction
